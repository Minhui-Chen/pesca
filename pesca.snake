# data
## gwas
try:
    gwas_eur
    gwas_eas
except NameError:
    raise Exception('GWAS files for EUR and EAS are not given.')

## ld score
### download LD scores from Alkes group https://github.com/bulik/ldsc
ld_eur_ln = 'https://data.broadinstitute.org/alkesgroup/LDSCORE/eur_w_ld_chr.tar.bz2'
ld_eas_ln = 'https://data.broadinstitute.org/alkesgroup/LDSCORE/eas_ldscores.tar.bz2'

rule pesca_download_LDscore:
    output:
        ld_eur = 'data/pesca/ldsc/eur/1.l2.ldscore.gz',
        ld_eas = 'data/pesca/ldsc/eas/1.l2.ldscore.gz',
    params:
        ld_eur_ln = ld_eur_ln,
        ld_eas_ln = ld_eas_ln,
    shell:
        '''
        eur_dir=$(dirname {output.ld_eur})
        cd $eur_dir
        wget {params.ld_eur_ln}
        tar xvjf eur_w_ld_chr.tar.bz2
        mv eur_w_ld_chr/* .
        rm -r eur_w_ld_chr
        rm eur_w_ld_chr.tar.bz2
        cd -

        eas_dir=$(dirname {output.ld_eas})
        cd $eas_dir
        wget {params.ld_eas_ln}
        tar xvjf eas_ldscores.tar.bz2
        mv eas_ldscores/* .
        rm -r eas_ldscores
        rm eas_ldscores.tar.bz2
        '''

rule pesca_LDscore_rsID2SNP_step1:
    input:
        ld = 'data/pesca/ldsc/{popu}/{chr}.l2.ldscore.gz',
        hg19 = 'data/pesca/ldsc/common_all_20180423.vcf.gz',
    output:
        snps = 'staging/pesca/ldsc/ldscore/{popu}/{chr}.l2.snps',
        bim = 'staging/pesca/ldsc/ldscore/{popu}/{chr}.l2.bim',
    params:
        bfile = lambda wildcards, output: re.sub('.bim$', '', output.bim),
    resources:
        mem_per_cpu = '3000',
    shell:
        '''
        zcat {input.ld} |tail -n +2 | cut -f2 > {output.snps}
        module load plink
        plink --memory 2000 --vcf {input.hg19} --chr {wildcards.chr} --extract {output.snps} \
                --keep-allele-order --make-just-bim \
                --allow-no-samples \
                --out {params.bfile}
        '''

localrules: pesca_LDscore_rsID2SNP_step2
rule pesca_LDscore_rsID2SNP_step2:
    input:
        ld = 'data/pesca/ldsc/{popu}/{chr}.l2.ldscore.gz',
        bim = 'staging/pesca/ldsc/ldscore/{popu}/{chr}.l2.bim',
    output:
        ld = 'data/pesca/ldsc/{popu}_rsID2SNP/{chr}.l2.ldscore.gz',
    run:
        import sys, gzip
        rsID2snp = {}
        for line in open( input.bim ):
            line = line.strip().split()
            rsID2snp[line[1]] = '_'.join( [line[0], line[3], line[-1], line[-2]] )

        with gzip.open( output.ld, 'wt' ) as f:
            f.write( gzip.open( input.ld, 'rt' ).readline() )
            for line in gzip.open( input.ld, 'rt' ).readlines()[1:]:
                line = line.strip().split()
                if line[1] in rsID2snp.keys():
                    line[1] = rsID2snp[line[1]]
                    f.write('\t'.join(line)+'\n')

localrules: pesca_LDscore_rsID2SNP_step3
rule pesca_LDscore_rsID2SNP_step3:
    input:
        M = 'data/pesca/ldsc/{popu}/{chr}.l2.M_5_50',
    output:
        M = 'data/pesca/ldsc/{popu}_rsID2SNP/{chr}.l2.M_5_50',
    shell:
        'cp {input.M} {output.M}'

# step 0: estimate heritability using LDSC
rule pesca_h2:
    input:
        gwas = 'data/pesca/ldsc/{popu}.sumstats.gz',
        ld = expand('data/pesca/ldsc/{{popu}}_rsID2SNP/{chr}.l2.ldscore.gz', chr=range(1,23)),
        M = expand('data/pesca/ldsc/{{popu}}_rsID2SNP/{chr}.l2.M_5_50', chr=range(1,23)),
    output:
        h2 = 'analysis/pesca/ldsc/h2/{popu}.log',
    params:
        out = lambda wildcards, output: re.sub('.log$', '', output.h2),
        samp_prev = lambda wildcards: {'eur':0.0312, 'eas':0.5071}[wildcards.popu],
        pop_prev = lambda wildcards: {'eur':0.0312, 'eas':0.0312}[wildcards.popu],
    shell:
        '''
        module load python/anaconda-2020.02
        source activate ldsc
        ~/programs/ldsc/ldsc.py \
                --h2 {input.gwas} \
                --ref-ld-chr $(dirname {input.ld[0]})/ \
                --w-ld-chr $(dirname {input.ld[0]})/ \
                --out {params.out} \
                --samp-prev {params.samp_prev} \
                --pop-prev {params.pop_prev}
        '''

# step 1: extract summary statistics and LD of each region for each chromosome
